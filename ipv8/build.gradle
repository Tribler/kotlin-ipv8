import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'kotlin'
    id 'java-library'
    id 'jacoco'
    id 'org.jetbrains.dokka'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'com.squareup.sqldelight'
}

dokkaHtml {
    outputDirectory.set(file("$buildDir/dokka"))
}

sqldelight {
    Database {
        packageName = "nl.tudelft.ipv8.sqldelight"
        sourceFolders = ["sqldelight"]
        schemaOutputDirectory = file("src/main/sqldelight/databases")
    }
}

ktlint {
    version = "$ktlint_version"
    android = true
    outputToConsole = true
    ignoreFailures = false
    filter {
        // https://github.com/JLLeitschuh/ktlint-gradle/issues/97
        exclude { "**/generated/**" }
        // https://github.com/JLLeitschuh/ktlint-gradle/issues/266
        exclude { element -> element.file.path.contains("generated/") }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
        html.enabled true
    }

    // TODO: exclude generated files
    afterEvaluate {
        getClassDirectories().setFrom(files(classDirectories.files.collect {
            fileTree(
                dir: it,
                exclude: ["**/sqldelight/**"]
            )
        }))
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Network utils
    implementation 'commons-net:commons-net:3.9.0'

    // Crypto
    implementation "com.goterl:lazysodium-java:5.1.4"

    // Logging
    implementation 'io.github.microutils:kotlin-logging:1.7.7'

    // JSON
    implementation 'org.json:json:20220320'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.mockk:mockk:1.10.4"
    testImplementation "com.squareup.sqldelight:sqlite-driver:$sqldelight_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"


    // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk15on
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15to18', version: '1.70'
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions.freeCompilerArgs += [
        "-opt-in=kotlin.ExperimentalUnsignedTypes",
        "-Werror" // Set Kotlin compiler warnings as errors
    ]
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11.toString()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}
